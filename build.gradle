/*
 * Copyright 2019, Perfect Sense, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

def releaseVersion = '0.99.3'
def releaseBuild = false;

if (System.getenv('TRAVIS_BRANCH') && System.getenv('TRAVIS_PULL_REQUEST') == 'false') {
    defaultTasks 'build', 'publish'

    releaseBuild = System.getenv('TRAVIS_BRANCH').startsWith("release/");
} else {
    defaultTasks 'build', 'publishToMavenLocal'
}

group = 'gyro'
version = releaseBuild ? releaseVersion : releaseVersion + '-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        url 'https://artifactory.psdops.com/public'
    }

    maven {
        url 'https://artifactory.psdops.com/gyro-snapshots'
    }

    maven {
        url 'https://artifactory.psdops.com/gyro-releases'
    }
}

configurations {
    gyroDoclet
}

/*shadowJar {
    classifier = ''
    include '*.jar'
    include 'com/azure/**'
    include 'gyro/azure/**'
    include 'com/fasterxml/**'

    // Relocating common dependencies
    relocate 'com.azure', 'shaded.gyro.azure'
    relocate 'com.fasterxml', 'shaded.gyro.aws.fasterxml'
}

build.dependsOn shadowJar*/

def azureSdkVersion = '1.41.2'

dependencies {
    api 'gyro:gyro-core:0.99.6' + (releaseBuild ? '' : '-SNAPSHOT')

    implementation 'com.psddev:dari-util:3.3.607-xe0f27a'

    implementation 'com.azure.resourcemanager:azure-resourcemanager-kubernetesconfiguration:1.0.0-beta.2'
    implementation 'com.azure.resourcemanager:azure-resourcemanager-resources:2.10.0'
    implementation 'com.azure.resourcemanager:azure-resourcemanager:2.10.0'
    implementation 'com.azure:azure-identity:1.4.2'
    implementation 'com.azure:azure-core-http-okhttp:1.7.4'
    implementation 'com.azure:azure-core:1.24.0'
    implementation 'com.azure:azure-containers-containerregistry:1.0.0-beta.1'

    gyroDoclet 'gyro:gyro-doclet:1.0.0'
}

task referenceDocs(type: Javadoc) {
    title = null // Prevents -doctitle and -windowtitle from being passed to GyroDoclet
    source = sourceSets.main.allJava
    classpath = configurations.runtimeClasspath
    options.doclet = "gyro.doclet.GyroDoclet"
    options.docletpath = configurations.gyroDoclet.files.asType(List)
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            credentials {
                username System.getenv('GRADLE_DEPLOY_USER')
                password System.getenv('GRADLE_DEPLOY_PASSWORD')
            }

            if (releaseBuild) {
                url 'https://artifactory.psdops.com/gyro-releases'

            } else {
                url 'https://artifactory.psdops.com/gyro-snapshots'
            }
        }
    }
}
