plugins {
    id 'java-library'
    id 'maven-publish'
}

if (System.getenv('TRAVIS_BRANCH') && System.getenv('TRAVIS_PULL_REQUEST') == 'false') {
    defaultTasks 'build', 'publish'
} else {
    defaultTasks 'build', 'publishToMavenLocal'
}

group = 'gyro'
version = '0..99.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        url 'https://artifactory.psdops.com/public'
    }

    maven {
        url 'https://artifactory.psdops.com/gyro-snapshots'
    }
}

configurations {
    gyroDoclet
}

def azureSdkVersion = '1.24.1'

dependencies {
    api 'gyro:gyro-core:0.99.0-SNAPSHOT'

    implementation 'com.psddev:dari-util:3.3.607-xe0f27a'
    implementation 'com.google.guava:guava:23.0'
    implementation 'com.microsoft.azure:adal4j:1.6.3'
    implementation "com.microsoft.azure:azure:${azureSdkVersion}"
    implementation "com.microsoft.azure:azure-mgmt-resources:${azureSdkVersion}"
    implementation "com.microsoft.azure:azure-mgmt-network:${azureSdkVersion}"
    implementation "com.microsoft.azure:azure-mgmt-compute:${azureSdkVersion}"
    implementation "com.microsoft.azure:azure-mgmt-dns:${azureSdkVersion}"
    implementation 'com.microsoft.azure:azure-storage:8.0.0'
    implementation 'com.github.seancfoley:ipaddress:5.0.2'

    gyroDoclet 'gyro:gyro-doclet:0.99.0-SNAPSHOT'
}

task referenceDocs(type: Javadoc) {
    title = null // Prevents -doctitle and -windowtitle from being passed to GyroDoclet
    source = sourceSets.main.allJava
    classpath = configurations.runtimeClasspath
    options.doclet = "gyro.doclet.GyroDoclet"
    options.docletpath = configurations.gyroDoclet.files.asType(List)
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            credentials {
                username System.getenv('GRADLE_DEPLOY_USER')
                password System.getenv('GRADLE_DEPLOY_PASSWORD')
            }

            if (project.version.endsWith('-SNAPSHOT')) {
                url 'https://artifactory.psdops.com/gyro-snapshots'

            } else {
                url 'https://artifactory.psdops.com/gyro-releases'
            }
        }
    }
}
