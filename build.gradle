/*
 * Copyright 2019, Perfect Sense, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java-library'
    id 'maven-publish'
}

def releaseVersion = '1.0.0'
def releaseBuild = false;

if (System.getenv('CI')) {
    defaultTasks 'build', 'publish'

    releaseBuild = System.getenv('GITHUB_ACTIONS_PULL_REQUEST') == '' &&
            System.getenv('GITHUB_REF').startsWith("refs/tags/release/");
} else {
    defaultTasks 'build', 'publishToMavenLocal'
}

group = 'gyro'
version = releaseBuild ? releaseVersion : releaseVersion + '-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        url 'https://artifactory.psdops.com/public'
    }

    maven {
        url 'https://artifactory.psdops.com/gyro-snapshots'
    }

    maven {
        url 'https://artifactory.psdops.com/gyro-releases'
    }
}

configurations {
    gyroDoclet
}

def azureSdkVersion = '1.41.2'

dependencies {
    api 'gyro:gyro-core:1.1.2'

    implementation 'com.psddev:dari-util:3.3.607-xe0f27a'
    implementation 'com.google.guava:guava:31.1-jre'

    implementation enforcedPlatform('com.azure:azure-sdk-bom:1.2.25')
    implementation 'com.azure.resourcemanager:azure-resourcemanager:2.40.0'
    implementation 'com.azure.resourcemanager:azure-resourcemanager-communication:2.1.0'
    implementation 'com.azure:azure-security-keyvault-certificates'
    implementation 'com.azure:azure-security-keyvault-keys'
    implementation 'com.azure:azure-security-keyvault-secrets'
    implementation 'com.azure:azure-data-tables'
    implementation 'com.azure:azure-storage-queue'
    implementation 'com.azure:azure-storage-file-share'
    implementation 'com.azure:azure-storage-blob'
    implementation 'com.azure:azure-identity'
    implementation 'com.azure:azure-core-http-okhttp'
    runtimeOnly 'com.nimbusds:oauth2-oidc-sdk:9.35'

    gyroDoclet 'gyro:gyro-doclet:1.0.0'
}

task referenceDocs(type: Javadoc) {
    title = null // Prevents -doctitle and -windowtitle from being passed to GyroDoclet
    source = sourceSets.main.allJava
    classpath = configurations.runtimeClasspath
    options.doclet = "gyro.doclet.GyroDoclet"
    options.docletpath = configurations.gyroDoclet.files.asType(List)
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            credentials {
                username System.getenv('GRADLE_DEPLOY_USER')
                password System.getenv('GRADLE_DEPLOY_PASSWORD')
            }

            if (releaseBuild) {
                url 'https://artifactory.psdops.com/gyro-releases'

            } else {
                url 'https://artifactory.psdops.com/gyro-snapshots'
            }
        }
    }
}
